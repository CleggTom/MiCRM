#defaults
"""
default params
"""
default_m(N,M,kw) = ones(N)
default_ρ(N,M,kw) = ones(M) * M
default_ω(N,M,kw) = ones(M)
default_u(N,M,kw) = copy(rand(Distributions.Dirichlet(M,1.0), N)')
default_l(N,M,kw) = copy(rand(Distributions.Dirichlet(M,1.0), M)') .* kw[:λ]

"""
    generate_params(N,M;f_m = default_m, f_ρ = default_ρ, 
        f_ω = default_ω, f_u = default_u, 
        f_l = default_l, kwargs...)

Function to generate parameter sets for MiCRM simualtions. Requires the system size is defined as well as the functions to generate the actual parameters used in the simulations. Extra arguments can also be passed via the `kwargs` argument. 

By default the function generates a random MiCRM system with no structure and uptake and leakage matricies generated by Dirichlet distribitions. 

The function returns a `NamedTuple` with all the parameters as well as a `kw` entry which itself is a `NamedTuple` with the additional arguments provided.
"""
function generate_params(N,M;f_m = default_m, f_ρ = default_ρ, f_ω = default_ω, f_u = default_u, f_l = default_l, kwargs...)
    kw = Dict{Symbol,Any}(kwargs)
    #consumers
    m = f_m(N,M,kw)
    #resources
    ρ = f_ρ(N,M,kw)
    ω = f_ω(N,M,kw)
    #uptake & leakage
    u = f_u(N,M,kw)
    l = f_l(N,M,kw)

    return (N = N, M = M, u = u, m = m, ρ = ρ, ω = ω, l = l, kw = NamedTuple(zip(keys(kwargs),values(kwargs))))
end

#uptake functions
"""
    modular_uptake(N,M; N_modules = 2, s_ratio = 10.0)

Function that generates a modular uptake matrix for a system with `N` consumers and `M` resources. The matrix is generated using a `Dirichlet` distribution such that the uptake of all consumers sums to 1. 
    
The number of modules determines how many groups of resources the consumers are specialised over. For example if `N_modules = 2` then the resources will be split into two groups with half the consumers specialising on one and half on the other. 

The degree of specialisation is determined by the `s_ratio` value. This controls the relative value of the dirchlet `α` parameters which determine how the probabiltiy density function is distributed over the different resources. Specialisation is obtained by setting the `α` values for resources that specialists consume to higher values meaning they have a higher probablity of a larger value. When `s_ratio = 1` the proabailtiy is uniform and all resources are equally likely to be consumed. When `s_ratio >1` then consumers are more likely to consume resources within thier module. 
"""
function modular_uptake(N,M; N_modules = 2, s_ratio = 10.0)
       @assert N_modules <= M
    
    #module sizes for consumers and resources
    module_C = N ÷ N_modules
    module_R = M ÷ N_modules

    #preallocate uptake matrix
    u = zeros(N,M)

    #loop over modules
    for g = 1:N_modules
        indx_C = 1 + (g-1)*module_C : module_C + (g-1)*module_C
        indx_R = 1 + (g-1)*module_R : module_R + (g-1)*module_R

        if g == N_modules
            indx_C = 1 + (g-1)*module_C : N
            indx_R = 1 + (g-1)*module_R : M
        end

        #calculate prob vec
        a_vec = ones(M)
        a_vec[indx_R] .*= s_ratio

        d = Dirichlet(a_vec)
        x = rand(d, length(indx_C))
        # println(indx_C, size(x')," ",size(u))

        u[indx_C, :] .= x'
    end
    
    return(u)
end

"""
    modular_uptake(N,M,kw)

Wrapper for the modular uptake to allow it to be used in the `generate_params` function.
"""
function modular_uptake(N,M,kw)
    modular_uptake(N,M,N_modules = kw[:N_modules], s_ratio = kw[:s_ratio])
end
"""
    modular_leakage(M; N_modules = 2, s_ratio = 10.0, λ = 0.5)

Function that generates a modular leakage matrix for a system with `M` resources with a directional leakage structure. The matrix is generated using a `Dirichlet` distribution such that the leakage of all resources sums to `λ`. 
    
The number of modules determines how many groups of resources they are split into. For example if `N_modules = 5` then the resources will be split into five groups with the first group of resources tending to leak to the second, the second to the third and so on. 
        
The degree of to which resources leak in this constrained way (verses randomly across all resources) is determined by the `s_ratio` value. This controls the relative value of the dirchlet `α` parameters which determine how the probabiltiy density function is distributed over the different resources. A greater probabiltiy of leakage from one resource to another is obtained by setting the `α` values for resources to higher values. When `s_ratio = 1` the proabailtiy is uniform and all resources are equally likely to be leaked to each other. When `s_ratio >1` then resources are more likely to leak to resources thier own or the next module in the sequence. 
"""
function modular_leakage(M; N_modules = 2, s_ratio = 10.0, λ = 0.5)
    @assert N_modules <= M
    
    #module  sizes for consumers and resources
    module_R = M ÷ N_modules

    #preallocate leakage matrix
    l = ones(M,M)

    #loop over modules
    for g = 1:N_modules
        for h = 1:N_modules
            indx_R1 = 1 + (g-1)*module_R : (g == N_modules ? M : module_R + (g-1)*module_R)
            indx_R2 = 1 + (h-1)*module_R : (h == N_modules ? M : module_R + (h-1)*module_R)

            if h == g+1 || h == g
                #calculate prob vec
                l[indx_R1, indx_R2] .*= s_ratio
            end
        
        end
    end

    for i = 1:M
        l[i,:] = rand(Dirichlet(l[i,:]), 1)
    end

    return(l .* λ)
end

"""
    modular_leakage(N,M,kw::Dict{Symbol, Any})

Wrapper for the modular leakage to allow it to be used in the `generate_params` function.
"""
function modular_leakage(N,M,kw::Dict{Symbol, Any})
    modular_leakage(M; N_modules = kw[:N_modules], s_ratio = kw[:s_ratio], λ = kw[:λ])
end
